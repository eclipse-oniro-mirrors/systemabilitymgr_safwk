# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

rust_cxx("system_ability_fwk_rust_gen") {
  sources = [ "src/wrapper.rs" ]
}

ohos_static_library("system_ability_fwk_rust_cxx") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
    cfi_no_nvcall = true
    blocklist = "../../../../cfi_blocklist.txt"
  }

  sources = [ "src/cxx/system_ability_wrapper.cpp" ]
  sources += get_target_outputs(":system_ability_fwk_rust_gen")

  include_dirs = [
    "${target_gen_dir}/src",
    "include",
  ]
  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
    "samgr:samgr_proxy",
  ]
  deps = [
    ":system_ability_fwk_rust_gen",
    "../../safwk:system_ability_fwk",
  ]

  if (defined(global_parts_info) &&
      !defined(global_parts_info.third_party_rust_cxx)) {
    deps += [ "//third_party/rust/crates/cxx:cxx_cppdeps" ]
  } else {
    external_deps += [ "rust_cxx:cxx_cppdeps" ]
  }

  subsystem_name = "systemabilitymgr"
  part_name = "safwk"
}

ohos_rust_shared_library("system_ability_fwk_rust") {
  crate_name = "system_ability_fwk"

  sources = [ "src/lib.rs" ]

  rustflags = [ "-Zstack-protector=all" ]

  deps = [ ":system_ability_fwk_rust_cxx" ]

  external_deps = [
    "hilog:hilog_rust",
    "ipc:ipc_rust",
  ]

  if (defined(global_parts_info) &&
      !defined(global_parts_info.third_party_rust_cxx)) {
    deps += [ "//third_party/rust/crates/cxx:lib" ]
  } else {
    external_deps += [ "rust_cxx:lib" ]
  }

  install_images = [ system_base_dir ]

  subsystem_name = "systemabilitymgr"
  part_name = "safwk"
}
