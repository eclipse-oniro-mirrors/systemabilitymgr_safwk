# Copyright (c) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//build/ohos_var.gni")

################################################################################

config("system_ability_config") {
  visibility = [ ":*" ]
  visibility += [ "//foundation/ability/ability_runtime/test/moduletest/ability_record_test/*" ]
  include_dirs = [
    "../../../services/safwk/include",
    "//foundation/systemabilitymgr/safwk/interfaces/innerkits/safwk",
  ]
}

config("system_ability_all_deps_config") {
  include_dirs =
      [ "//foundation/systemabilitymgr/safwk/interfaces/innerkits/safwk" ]
}

ohos_shared_library("system_ability_fwk") {
  version_script = "libsystem_ability_fwk.versionscript"
  sources = [
    "../../../services/safwk/src/local_ability_manager.cpp",
    "../../../services/safwk/src/local_ability_manager_stub.cpp",
    "../../../services/safwk/src/system_ability.cpp",
    "../../../services/safwk/src/system_ability_ondemand_reason.cpp",
  ]

  configs =
      [ "//foundation/systemabilitymgr/safwk/test/resource:coverage_flags" ]

  public_configs = [
    ":system_ability_config",
    "//foundation/systemabilitymgr/safwk/test/resource:coverage_flags",
  ]
  all_dependent_configs = [ ":system_ability_all_deps_config" ]
  if (is_standard_system) {
    external_deps = [
      "c_utils:utils",
      "hilog:libhilog",
      "hitrace:hitrace_meter",
      "ipc:ipc_core",
      "samgr:samgr_common",
      "samgr:samgr_proxy",
    ]
  }

  install_images = [ system_base_dir ]
  relative_install_dir = "platformsdk"
  innerapi_tags = [
    "platformsdk_indirect",
    "sasdk",
  ]
  part_name = "safwk"
  subsystem_name = "systemabilitymgr"
}

ohos_rust_shared_library("system_ability_fwk_rust") {
  sources = [
    "rust/src/lib.rs",
    "rust/src/sa_binding.rs",
    "rust/src/safwk/macros.rs",
    "rust/src/safwk/mod.rs",
    "rust/src/safwk/r_system_ability.rs",
  ]

  deps = [
    "//foundation/systemabilitymgr/safwk/services/safwk:system_ability_fwk_c",
  ]

  external_deps = [
    "hilog:hilog_rust",
    "ipc:ipc_rust",
    "samgr:rust_samgr",
  ]

  crate_name = "system_ability_fwk_rust"
  crate_type = "dylib"

  install_images = [ system_base_dir ]
  relative_install_dir = "chipset-pub-sdk"
  subsystem_name = "systemabilitymgr"
  part_name = "safwk"
}
################################################################################
