# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

################################################################################
config("config_safwk") {
  visibility = [ ":*" ]
  include_dirs = [
    "//foundation/systemabilitymgr/safwk/services/safwk/include",
    "//foundation/systemabilitymgr/safwk/interfaces/innerkits/safwk",
  ]
}

config("libsafwk_c_private_config") {
  cflags_cc = [ "-O2" ]
}

ohos_executable("sa_main") {
  install_enable = true
  sources = [ "src/main.cpp" ]

  configs = [
    ":config_safwk",
    "//foundation/systemabilitymgr/safwk/test/resource:coverage_flags",
  ]
  configs += [ "//build/config/gcc:symbol_visibility_hidden" ]

  ldflags = [
    "-rdynamic",
    "-Wl,-z,max-page-size=4096",
    "-Wl,-z,separate-code",
  ]

  deps = [
    "../../interfaces/innerkits/safwk:system_ability_fwk",
    "//foundation/systemabilitymgr/safwk/interfaces/innerkits/safwk/rust:system_ability_fwk_rust",
  ]

  if (is_standard_system) {
    external_deps = [
      "c_utils:utils",
      "hilog:libhilog",
      "init:libbegetutil",
      "ipc:ipc_single",
      "samgr:samgr_common",
    ]
  }

  defines = []
  if (use_musl) {
    if (use_jemalloc && use_jemalloc_dfx_intf) {
      defines += [ "CONFIG_USE_JEMALLOC_DFX_INTF" ]
    }
  }

  part_name = "safwk"
  subsystem_name = "systemabilitymgr"
}
################################################################################
