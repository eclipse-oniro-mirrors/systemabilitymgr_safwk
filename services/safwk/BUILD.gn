# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

################################################################################
config("config_safwk") {
  visibility = [ ":*" ]
  include_dirs = [
    "//foundation/systemabilitymgr/safwk/services/safwk/include",
    "//foundation/systemabilitymgr/samgr/services/lsamgr/include",
    "//foundation/systemabilitymgr/safwk/interfaces/innerkits/safwk",
    "//foundation/communication/ipc/interfaces/innerkits/ipc_core/include",
  ]
}

config("libsafwk_c_private_config") {
  cflags_cc = [ "-O2" ]
}

ohos_shared_library("system_ability_fwk_c") {
  include_dirs = [
    "//foundation/systemabilitymgr/safwk/services/safwk/c_wrapper/include",
    "//foundation/systemabilitymgr/safwk/services/safwk/include",
    "//foundation/systemabilitymgr/samgr/services/lsamgr/include",
  ]
  sources = [ "//foundation/systemabilitymgr/safwk/services/safwk/c_wrapper/source/c_system_ability.cpp" ]

  configs = [ ":libsafwk_c_private_config" ]

  deps = [ "//foundation/systemabilitymgr/safwk/interfaces/innerkits/safwk:system_ability_fwk" ]

  external_deps = [
    "c_utils:utils",
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_single",
  ]

  output_extension = "so"
  install_images = [ system_base_dir ]
  relative_install_dir = "chipset-pub-sdk"
  subsystem_name = "systemabilitymgr"
  part_name = "safwk"
}

ohos_executable("sa_main") {
  install_enable = true
  sources = [ "src/main.cpp" ]

  configs = [
    ":config_safwk",
    "//foundation/systemabilitymgr/safwk/test/resource:coverage_flags",
  ]
  configs += [ "//build/config/gcc:symbol_visibility_hidden" ]

  ldflags = [ "-rdynamic" ]

  deps = [
    "../../interfaces/innerkits/safwk:system_ability_fwk",
    "//foundation/systemabilitymgr/safwk/interfaces/innerkits/safwk:system_ability_fwk_rust",
  ]

  if (is_standard_system) {
    external_deps = [
      "c_utils:utils",
      "hiviewdfx_hilog_native:libhilog",
      "init:libbegetutil",
      "samgr:samgr_common",
    ]
  }

  part_name = "safwk"
  subsystem_name = "systemabilitymgr"
}
################################################################################
